plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.3.3.RELEASE'

    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    // Build uber-jar
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

group 'ru.otus'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_13
targetCompatibility = JavaVersion.VERSION_13

repositories {
    mavenCentral()
}

idea {
    project {
        languageLevel = 13
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.3.3.RELEASE")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.openjdk.jmh:jmh-core:${Versions.jmh}")
            dependency("org.openjdk.jmh:jmh-generator-annprocess:${Versions.jmh}")
            dependency("org.openjdk.jol:jol-core:${Versions.jol}")
            dependency("org.ow2.asm:asm-commons:${Versions.asm}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
            dependency("com.google.protobuf:protobuf-java-util:${Versions.protobuf}")
            dependency("javax.xml.bind:jaxb-api:${Versions.jaxbApi}")
            dependency("org.postgresql:postgresql:${Versions.postgresql}")
            dependency("org.testcontainers:junit-jupiter:${Versions.testcontainers}")
            dependency("org.testcontainers:postgresql:${Versions.testcontainers}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }

        resolutionStrategy {
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("org.javassist:javassist:3.24.0-GA")
        }
    }
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.with {
        encoding = 'UTF-8'
    }
}